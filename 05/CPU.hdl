// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // determine instruction
    Mux16(a[0..14]=instruction[0..14], b=aluOut, sel=instruction[15], out=aOrCInstr);
    Not(in=instruction[15], out=isAInstr);
    Or(a=isAInstr, b=instruction[5], out=aShouldLoad);
    ARegister(in=aOrCInstr, load=aShouldLoad, out=aRegOut, out[0..14]=addressM);

    // select memory value or address for ALU
    Not(in=instruction[12], out=shouldUseA);
    Or(a=isAInstr, b=shouldUseA, out=shouldLoadFromA);
    Mux16(a=inM, b=aRegOut, sel=shouldLoadFromA, out=mOrAVal);

    Not(in=isAInstr, out=isNotAInstr);
    And(a=isNotAInstr, b=instruction[4], out=dShouldLoad);
    DRegister(in=aluOut, load=dShouldLoad, out=dRegOut);
    // get control bits considering A instruction
    And(a=isNotAInstr, b=instruction[11], out=zx);
    And(a=isNotAInstr, b=instruction[10], out=nx);
    And(a=isNotAInstr, b=instruction[9], out=zy);
    And(a=isNotAInstr, b=instruction[8], out=ny);
    And(a=isNotAInstr, b=instruction[7], out=f);
    And(a=isNotAInstr, b=instruction[6], out=no);

    ALU(x=dRegOut, y=mOrAVal, zx=zx, nx=nx, zy=zy, ny=ny, f=f, no=no, out=aluOut, out=outM, zr=isZero, ng=isNeg);

    And(a=isNotAInstr, b=instruction[3], out=writeM);

    Not(in=isNeg, out=isNotNeg);
    Not(in=isZero, out=isNotZero);
    And(a=isNotNeg, b=isNotZero, out=isPos);

    And(a=isPos, b=instruction[0], out=JGT);
    And(a=isZero, b=instruction[1], out=JEQ);
    And(a=isNeg, b=instruction[2], out=JLT);

    // load from a if JEQ || JLT || JGT
    Or(a=JEQ, b=JLT, out=JLE);
    Or(a=JLE, b=JGT, out=loadFromA);

    And(a=isNotAInstr, b=loadFromA, out=load);
    Not(in=load, out=inc);
    PC(in=aRegOut, load=load, inc=inc, reset=reset, out[0..14]=pc);
}